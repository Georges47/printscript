plugins {
    id 'scala'
    id "com.github.maiflai.scalatest"
    id "org.scoverage"
    id 'cz.augi.gradle.scalafmt'
    id 'maven-publish'
}

task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks')}
    fileMode 0775
}

tasks.build.dependsOn installLocalGitHook

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/austral-ingsis/printscript-parser-common")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Georges47/printscript"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.13.6'
    testImplementation 'junit:junit:4.13.2'
    testImplementation group: 'org.scalatest', name: 'scalatest_2.13', version: '3.2.11'
    testImplementation 'org.scalatestplus:junit-4-13_2.13:3.2.11.0'
    testRuntimeOnly 'com.vladsch.flexmark:flexmark-all:0.60.0'
}